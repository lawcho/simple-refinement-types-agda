---
title: FoCAL project report
author: Lawrence Chonavel (with lots of help from Ulf Norell)
date: September 2022
geometry: margin=1cm
classoption: twocolumn
bibliography: biblio.bib
csl: ieee.csl
colorlinks: true
numbersections: true
autoSectionLabels: true
codeBlockCaptions: true
# Break long  lines in code blocks https://stackoverflow.com/a/48507868
# Put boxes round images : https://tex.stackexchange.com/a/526521
# Set up fallback font: https://tex.stackexchange.com/a/572220
header-includes: |
  ~~~{=tex}
  \usepackage{agda}
  \usepackage{fontspec}
  \usepackage{comment}

  \usepackage{fvextra}
  \DefineVerbatimEnvironment{Highlighting}{Verbatim}{breaklines,commandchars=\\\{\}}

  \usepackage[export]{adjustbox}
  \let\includegraphicsbak\includegraphics
  \renewcommand*{\includegraphics}[2][]{\includegraphicsbak[frame,#1]{#2}} 

  \usepackage{luaotfload}
  \directlua{luaotfload.add_fallback
   ("mycustomfallback",
    { "SymbolamonospacifiedforSourceCodePro:style=Regular;"
    , "NotoSansMono:style=Regular;"
    , "NotoSansMath:style=Regular;"
    }
   )}

  \defaultfontfeatures{RawFeature={fallback=mycustomfallback}}
  ~~~

abstract: |
  Type systems are intended to catch programmers' bugs, but sometimes the type systems themselves have bugs.
  I set out to find bugs in a pair of *refinement type systems* by Jhala et al. [@refinement-tut], producing along the way a *formalization* of the type systems and a *verified implementation* of their type checking and inference algorithms, all in Agda [@Agda].

---

[]: <> (Set fonts for main body of document)

\setmainfont{DejaVu Serif}
\setsansfont{DejaVu Sans}
\setmonofont{DejaVu Sans Mono}
\setmathfont{Latin Modern Math}

[]: <> (Set Agda code fonts)
[]: <> (https://agda.readthedocs.io/en/v2.6.2.2/tools/generating-latex.html)
\newfontfamily{\AgdaRegularFont}{Source Code Pro}
\newfontfamily{\AgdaMediumFont}{Source Code Pro Medium}
\newfontfamily{\AgdaLightFont}{Source Code Pro Light}
\renewcommand{\AgdaFontStyle}[1]{{\AgdaMediumFont{}#1}}
\renewcommand{\AgdaKeywordFontStyle}[1]{{\AgdaMediumFont{}#1}}
\renewcommand{\AgdaStringFontStyle}[1]{{\AgdaLightFont{}#1}}
\renewcommand{\AgdaCommentFontStyle}[1]{{\AgdaLightFont{}#1}}
\renewcommand{\AgdaBoundFontStyle}[1]{\AgdaRegularFont{}#1}


:::: hidden ::::
```agda
module report where
open import Util.All
```
::::::::::::::::


# Introduction

*Refinement type systems* allow programmers to write rich specifications for their programs by *refining* type signatures with logical predicates. For example, the annotation on `r_sq`'s return type asserts that `r_sq` produces only non-negative values.

~~~haskell
-- Compute modulus-squared of a vector
r_sq : Real √ó Real ‚Üí Real[v | v >= 0]
r_sq (x , y) = x * x + y * y
~~~

These refinement types are automatically checked at compile-time, and can be used in proving more complicated properties, e.g. that the call to `sqrt` is safe:

~~~haskell
primitive sqrt : Real[v | v >= 0] ‚Üí Real

-- Euclidean distance between two vectors
vec_dist : Real √ó Real ‚Üí Real √ó Real ‚Üí Real
vec_dist (x1 , y1) (x2 , y2) =
    sqrt $ r_sq ((x1 - x2) , (y1 - y2))
~~~

More information on programming with refinement types can be found at the liquidhaskell website [@lh-website], we now turn to how these type systems are *implemented*.

Jhala and Vazou [@refinement-tut] give a tutorial-style presentation of the theory behind refinement type systems, starting with a minimal STLC-like language and showing how to add features one by one.

Unfortunately, these type systems are described only semi-formally, using a combination of \LaTeX{} rules, pseudocode functions, and prose (e.g. [@fig:rules-syn; @fig:subst-defn; @fig:prose-defn]), which leave room for ambiguity and mistakes.

It's also not clear whether real (i.e. executable) implementations of the type systems (e.g. Sprite [@sprite]) correctly implement the rules in the paper.

# What have I built?

I have formalized the languages in [@refinement-tut] up to Lang-ŒªŒ≤, i.e. a simply-typed Œª-calculus with refinement types, branches, and recursion. My Agda formalization includes:

An implementation of Lang-ŒªŒ≤'s *typing rules*

: e.g. the paper's type-synthesis relation in [@fig:rules-syn] is formalized as the data type in [@lst:ex-rules-syn]

A proof-producing *type-checker*

: for Lang-ŒªŒ≤ programs (@sec:a-proof-producing-type-checker)

Fixes for various *bugs* in the paper

: ([@sec:bugs-found])

Source code for this formalization (and report) can be found at <https://github.com/lawcho/simple-refinement-types-agda>


![Paper's type-synthesis relation (defined using rule notation)](images/rules-syn.png){#fig:rules-syn}

![Paper's "capture avoiding substitution" (defined using pseudocode)](images/subst-defn.png){#fig:subst-defn}

![Fragment of paper's VC semantics (defined using prose)](images/prose-defn.png){#fig:prose-defn}

:::: hidden ::::
```agda
module Example-Synth where
  open import Lang-Œª.Refinement
  open import Lang-Œª.Type
  open import Lang-Œª.Term
  open import Lang-Œª.Typing
  open import Common.Example-Solver
  open With-SMT solver hiding (_‚ä¢_‚áí·µó_)

```
::::::::::::::::

```agda
  data _‚ä¢_‚áí·µó_ (Œì : Context)
    : Term ‚Üí Type ‚Üí Set
    where
      syn-var
          : ‚àÄ {x t ptr}
          ‚Üí lookup x Œì ‚â° yes (t , ptr)
          ---------------------------
          ‚Üí Œì ‚ä¢ var x ‚áí·µó t
      syn-con
          : ‚àÄ{c t}
          ‚Üí prim c ‚â° t
          ---------------
          ‚Üí Œì ‚ä¢ con c ‚áí·µó t
      syn-ann
          : ‚àÄ{e t k}
          ‚Üí Œì ‚ä¢ t :·µè k
          ‚Üí Œì ‚ä¢ e ‚áê·µó t
          ---------------
          ‚Üí Œì ‚ä¢ e ‚¶Ç t ‚áí·µó t
      syn-app
          : ‚àÄ {e x s t y}
          ‚Üí Œì ‚ä¢ e ‚áí·µó (x ‚¶Ç s ‚Üí' t)
          ‚Üí Œì ‚ä¢ var y ‚áê·µó s
          -------------------------------
          ‚Üí Œì ‚ä¢ e [ y ] ‚áí·µó (t [ x := y ]‚Çú)
```

: Formalization of the `Œì ‚ä¢ e ‚áí t` relation from [@fig:rules-syn] {#lst:ex-rules-syn}

# What worked well

Bug-fixes aside, my formalization follows the paper closely, and is quite concise. This is thanks to the following design decisions:

## A Proof-Producing Type-Checker

My type-checker always gives the correct answer, and can prove it!

We can know this from its type signature:

:::: hidden ::::
```agda
module Checker-Sig where
  open import Lang-ŒªŒ≤.Typing
  open import Common.Example-Solver
  open With-SMT solver
  open import Lang-ŒªŒ≤.Term
  open import Lang-ŒªŒ≤.Sugar
  postulate
```
::::::::::::::::

```agda
    check :
      (Œì : Context) ‚Üí
      (e : Term) ‚Üí
      (t : Type) ‚Üí
      Dec (Œì ‚ä¢ e ‚áê·µó t)
```

which says that `check` is a function that takes a context
`Œì`, a term `e`, and a type `t`, then
`Dec`ides `Œì ‚ä¢ e ‚áê·µó t`, i.e. produces either a typing
derivation for `Œì ‚ä¢ e ‚áê·µó t`, or a proof that no such typing
derivation exists.

:::: hidden ::::
```agda
module example1 where

  open import Lang-ŒªŒ≤.Typing
  open import Common.Example-Solver
  open With-SMT solver
  open import Lang-ŒªŒ≤.Algorithmic-Typing solver
  open import Lang-ŒªŒ≤.Refinement
  open import Lang-ŒªŒ≤.Sugar

  open import Common.VC-Syntax.Type
  open import Common.VC-Syntax.Predicate
  open import Common.VC-Syntax.Constraint
```
::::::::::::::::

Let's see it in action! Here's a Lang-ŒªŒ≤ program which is supposed to always return a non-negative number:

```agda
  abs-diff =
    ŒªŒª"x"‚àô ŒªŒª"y"‚àô
    let‚Çë "x‚â§y" := "x" ‚â§‚Çë "y" in‚Çë
    if‚Çë   "x‚â§y"
    then‚Çë "y" -‚Çë "x"
    else‚Çë "x" -‚Çë "y"
```

To prove that `abs-diff` has this property, we can encode the property as a type `ty`:

```agda
  ty =
    "x"‚¶Ç int ‚ü®‚ü© ‚Üí'
    "y"‚¶Ç int ‚ü®‚ü© ‚Üí'
    int ‚ü®"z"‚à£ -1 <·µ£ "z" ‚ü©
```

then construct a typing derivation proving that `abs-diff` has that type:

```agda
  typing-der : [] ‚ä¢ abs-diff ‚áê·µó ty
```

by calling `check`, and extracting its (positive) result:

```agda
  typing-der with
    of ∏ der ‚Üê proof $ check [] abs-diff ty
    = der
```

## Mocking SMT

To decide typing relations like those above, the type-`check`er depends on an external tool. Let's see how.

Following the typing rules, the type-checker breaks down the judgement `[] ‚ä¢ abs-diff ‚áê·µó ty` to subtyping constraints including `_c`:

```agda
  _c =
      (raw (var "x‚â§y" ‚ñπ == ‚óÉ true)
    ‚à∑ ("x‚â§y"‚¶Ç bool ‚ü®"b"‚à£ var "b"
        ‚ñπ == ‚óÉ ((var "x" ‚ñπ < ‚óÉ var "y")
        ‚ñπ ‚à® ‚óÉ   (var "x" ‚ñπ == ‚óÉ var "y"))‚ü©)
    ‚à∑ ("y"‚¶Ç int ‚ü®‚ü©) ‚à∑ ("x"‚¶Ç int ‚ü®‚ü©) ‚à∑ [])
    ‚ä¢  int ‚ü®"v"‚à£ var "v"
        ‚ñπ == ‚óÉ (var "y" ‚ñπ - ‚óÉ var "x")‚ü©
    ‚â∫: int ‚ü®"z"‚à£ -1 ‚ñπ < ‚óÉ var "z"‚ü©
```

which it passes if and only if a corresponding logical formula `_vc` is valid:

```agda
  _vc =
    ‚àÄ‚àÄ int ‚àô ‚àÄ‚àÄ int ‚àô ‚àÄ‚àÄ bool ‚àô
      (var ‚Üë0 ‚ñπ == ‚óÉ
      (     (var ‚Üë2 ‚ñπ < ‚óÉ  var ‚Üë1)
      ‚ñπ ‚à® ‚óÉ (var ‚Üë2 ‚ñπ == ‚óÉ var ‚Üë1))) ‚áí
        (var ‚Üë0 ‚ñπ == ‚óÉ true) ‚áí
          ‚àÄ‚àÄ int ‚àô
            (var ‚Üë0 ‚ñπ == ‚óÉ
            (var ‚Üë2 ‚ñπ - ‚óÉ var ‚Üë3)) ‚áí
              pred (-1 ‚ñπ < ‚óÉ var ‚Üë0)

```

Notice how `_vc` is quite complex: it contains many different logical connectives, and even statements about integer arithmetic.

To efficiently decide formulas like `_vc`, the checker queries an external tool -- an *SMT solver*.
Unfortunately, existing SMT solvers are extremely complex^[
Microsoft's [Z3]{.smallcaps} [@z3github] weighs in at over 400k lines of C++:  
`curl -L https://api.codetabs.com/v1/loc?github=Z3Prover/z3`],
making formalizing them a challenge.

I avoided this problem by instead *mocking* an SMT solver -- i.e. I implemented just enough of an SMT solver to cover all the queries that the example programs make.

The mock is too verbose to present here, but can be found in `Common.Example-Solver.agda`^[e.g. the query `_vc` is covered on lines 362-386].

## Mocking SMT *Soundly* 

The SMT solver mock might not be complete, but at least it never gives wrong answers to queries (i.e. it is "sound").

We can be sure that the mock is sound because (like `check`) it does not return a `Bool`, but rather a `Dec`ision (this time for VC validity):

:::: hidden ::::
```agda
module Type-Of-solver where
  open import Common.VC-Syntax.Constraint
  open import Common.VC-Semantics.Constraint
  postulate
```
::::::::::::::::

```agda
    smt-mock : (vc : VC) ‚Üí Dec (Valid vc)
```

## Mixed-Style VC Semantics

:::: hidden ::::
```agda
module Predicate-Denot-Sem {Œì} Œî where
  open import Common.VC-Syntax.Type
  open import Common.VC-Syntax.Predicate
  open import Common.VC-Semantics.Type
```
::::::::::::::::

For the SMT mock to produce VC validity (dis-)proofs, I first needed to define the type, `Valid`, of VC-validity proofs (aka the "semantics" of VCs).

This semantics is a bit unusual, since it mixes two different styles: VC constraints get a denotational semantics (i.e. a translation *function*), whereas VC predicates get a big-step semantics (i.e. a translation *relation*).

This is deliberate: the obvious denotational semantics for predicates is easy to give (@lst:vc-pred-denot-sem), but very awkward to write decision procedures for.

```agda
  ‚ü¶_‚üß‚Çö : ‚àÄ {œÑ} ‚Üí Predicate Œì œÑ ‚Üí ‚ü¶ œÑ ‚üß‚Çú
  ‚ü¶ true ‚üß‚Çö   = ùîπ.true
  ‚ü¶ false ‚üß‚Çö  = ùîπ.false
  ‚ü¶ ¬¨ p ‚üß‚Çö    = ùîπ.not ‚ü¶ p ‚üß‚Çö
  ‚ü¶ p‚ÇÅ ‚ñπ < ‚óÉ p‚ÇÇ ‚üß‚Çö = does (‚ü¶ p‚ÇÅ ‚üß‚Çö ‚Ñ§.<? ‚ü¶ p‚ÇÇ ‚üß‚Çö)
  ‚ü¶ p‚ÇÅ ‚ñπ > ‚óÉ p‚ÇÇ ‚üß‚Çö = does (‚ü¶ p‚ÇÇ ‚üß‚Çö ‚Ñ§.<? ‚ü¶ p‚ÇÅ ‚üß‚Çö)
  ‚ü¶ p‚ÇÅ ‚ñπ ‚àß ‚óÉ p‚ÇÇ ‚üß‚Çö = ‚ü¶ p‚ÇÅ ‚üß‚Çö ùîπ.‚àß ‚ü¶ p‚ÇÇ ‚üß‚Çö
  ‚ü¶ p‚ÇÅ ‚ñπ ‚à® ‚óÉ p‚ÇÇ ‚üß‚Çö = ‚ü¶ p‚ÇÅ ‚üß‚Çö ùîπ.‚à® ‚ü¶ p‚ÇÇ ‚üß‚Çö
  ‚ü¶ p‚ÇÅ ‚ñπ + ‚óÉ p‚ÇÇ ‚üß‚Çö = ‚ü¶ p‚ÇÅ ‚üß‚Çö ‚Ñ§.+ ‚ü¶ p‚ÇÇ ‚üß‚Çö
  ‚ü¶ p‚ÇÅ ‚ñπ - ‚óÉ p‚ÇÇ ‚üß‚Çö = ‚ü¶ p‚ÇÅ ‚üß‚Çö ‚Ñ§.- ‚ü¶ p‚ÇÇ ‚üß‚Çö
  ‚ü¶ p‚ÇÅ ‚ñπ == {ty} ‚óÉ p‚ÇÇ ‚üß‚Çö with ty
  ... | int  = does (‚ü¶ p‚ÇÅ ‚üß‚Çö ‚Ñ§.‚âü  ‚ü¶ p‚ÇÇ ‚üß‚Çö)
  ... | bool = does (‚ü¶ p‚ÇÅ ‚üß‚Çö ùîπ.‚âü  ‚ü¶ p‚ÇÇ ‚üß‚Çö)
  ‚ü¶ num x ‚üß‚Çö      = x
  ‚ü¶ var ptr ‚üß‚Çö    = follow ptr into Œî
  ‚ü¶ if p‚ÇÅ then p‚ÇÇ else p‚ÇÉ ‚üß‚Çö with ‚ü¶ p‚ÇÅ ‚üß‚Çö
  ... | ùîπ.true  = ‚ü¶ p‚ÇÇ ‚üß‚Çö
  ... | ùîπ.false = ‚ü¶ p‚ÇÉ ‚üß‚Çö
```

: Denotational semantics for VC predicates {#lst:vc-pred-denot-sem}

The problem is that `‚ü¶_‚üß‚Çö` produces expressions containing function calls, which block unification when placed in types, making predicate-value proofs difficult to split up^[e.g. `pf : ùîπ.true ‚â° does (x ‚Ñ§.‚âü y) ùîπ.‚àß does (y ‚Ñ§.‚âü num 4)` can't be matched (as `refl`) since Agda can't unify the LHS and RHS of the equation] or combine.

In contrast, big-step derivations can be split up by pattern-matching constructors. The constructors also make combining derivations easy.
This helps keep VC-validity proofs short & structured.


## Capture-avoiding Œ±-renaming

The typing rules use *capture-avoiding substitution*, a notoriously fickle algorithm (see [@sec:bugs-found]).

Fortunately, we don't actually need the full *substitution* algorithm -- the special case of *Œ±-renaming* is sufficient (and easier to implement). This is possible since Lang-ŒªŒ≤ is an *Administrative Normal Form* language^[i.e. a language where functions can only be applied to variables].

This formalization implements (capture-avoiding) Œ±-renaming using a brand new, stateless algorithm. Let's derive it now.

### The new algorithm

:::: hidden ::::
```agda
module Œ±-renaming-algorithm
    (String : Set)
    (_==_ : String ‚Üí String ‚Üí ùîπ)
  where
  infixr 25 ŒªŒª_‚àô_
  infixl 24 _[_‚Ü¶_] _[_‚Üî_] 

  infixr 10 _if_‚à£_
  infix 11 _otherwise
  _otherwise : ‚àÄ{‚Ñìa}{A : Set ‚Ñìa} ‚Üí A ‚Üí A
  _otherwise = id
  _if_‚à£_ : ‚àÄ{‚Ñìa}{A : Set ‚Ñìa} ‚Üí A ‚Üí ùîπ ‚Üí A ‚Üí A
  a if ùîπ.true ‚à£ _ = a
  _ if ùîπ.false ‚à£ a = a

  _[_‚Üî_]' : String ‚Üí String ‚Üí String ‚Üí String
```
::::::::::::::::

For simplicity, we'll work with the untyped lambda calculus, which has only two constructors:

```agda
  data Exp : Set where
    var : String ‚Üí Exp
    ŒªŒª_‚àô_ : String ‚Üí Exp ‚Üí Exp 
```

We are going to implement an Œ±-renaming function

```agda
  _[_‚Ü¶_] : Exp ‚Üí String ‚Üí String ‚Üí Exp
```

such that `e [ old ‚Ü¶ new ]` replaces all free occurrences of `old` with `new` in `e`, without changing `e`'s binding structure.

The case for variables is easy

```agda
  var x [ old ‚Ü¶ new ]
    = var new     if x == old
    ‚à£ var x       otherwise
```

the case for Œª-bindings is recursive, but straightforward enough except for the capture-avoiding case

```agda
  ŒªŒª x ‚àô e [ old ‚Ü¶ new ]
    = ŒªŒª x ‚àô e                         if x == old
    ‚à£ {! must not capture old here !} if x == new
    ‚à£ ŒªŒª x ‚àô (e [ old ‚Ü¶ new ])         otherwise
```

It is *not* correct to fill the hole with `ŒªŒª x ‚àô (e [ old ‚Ü¶ new ])`, since this will capture all free occurrences of `old` in `e` (they will become bound by `ŒªŒª x`, but we want them kept free).

The usual solution is to generate a "fresh" variable `tmp` (i.e. a variable that is not free in `e`), and then return `ŒªŒª tmp ‚àô (e [ new ‚Ü¶ tmp ] [ old ‚Ü¶ new ])`. Unfortunately, generating "fresh" variables is quite intricate, requiring an extra traversal of `e`.

Let's derive an equivalent but simpler algorithm, by equational reasoning:

~~~eq
ŒªŒª tmp ‚àô
  (e [new ‚Ü¶ tmp][old ‚Ü¶ new])
      ==Œ±==
ŒªŒª old ‚àô
  (e [new ‚Ü¶ tmp][old ‚Ü¶ new][tmp ‚Ü¶ old])
      ==Œ±==
ŒªŒª old ‚àô
  (e [ new ‚Üî old ])
~~~

The first equation is justified by the fact that `old` is fresh in `e [ new ‚Ü¶ tmp ] [ old ‚Ü¶ new ]` (thanks to the last renaming). 
The second equation is justified by observing that `[new ‚Ü¶ tmp][old ‚Ü¶ new][tmp ‚Ü¶ old]` just swaps `new` and `old`.

So overall, we have just reduced the tricky case of Œ±-renaming to Œ±-swapping, which can be implemented recursively:

```agda
  _[_‚Üî_] : Exp ‚Üí String ‚Üí String ‚Üí Exp
  var x [ v1 ‚Üî v2 ]
    = var (x [ v1 ‚Üî v2 ]')
  ŒªŒª x ‚àô e [ v1 ‚Üî v2 ]
    = ŒªŒª (x [ v1 ‚Üî v2 ]') ‚àô (e [ v1 ‚Üî v2 ])
```

where `_[_‚Üî_]'` is (non-recursive) Œ±-swapping on strings:

```agda
  s [ v1 ‚Üî v2 ]'
    = v2        if s == v1
    ‚à£ v1        if s == v2
    ‚à£ s         otherwise
```

### Correctness

I have not proved the correctness of the algorithm formally. Some unit tests can be found in `Lang-Œª.Type.agda`.

# Bugs found

The formalization also unearthed several mistakes in the paper.

Most of these involve the translation of subtyping constraints into SMT-ready verification conditions ([@fig:rules-ent-subty]).

![The rules for subtyping](images/rules-ent-subty.png){#fig:rules-ent-subty}

## Missing rule

The paper's $‚ä¢$ relation ([@fig:rules-ent-subty]) is missing a rule.

There are rules for when Œì is empty ([Ent-Emp]{.smallcaps}), or when the last type in Œì is a base type ([Ent-Ext]{.smallcaps}), but no rule for when Œì contains a function type.

As a consequence, higher-order functions such as `flip` do not type-check:

::: hidden :::
```agda
module Example-Broken-HOF where
  open import Common.Example-Solver
  open import Lang-ŒªŒ≤.Type
  open import Lang-ŒªŒ≤.Term
  open import Lang-ŒªŒ≤.Typing
```
::::::::::::::

```agda
  flip =
    ŒªŒª "f" ‚àô ŒªŒª "x" ‚àô ŒªŒª "y" ‚àô
    var "f" ["y"] ["x"]
```

To fix this bug, I added a rule that ignores function types in the context:

::: hidden :::
```agda
  data _‚ä¢_ : Context ‚Üí Untyped-Constraint ‚Üí Set where
```
::::::::::::::

```agda
    ent-fun
      : ‚àÄ{Œì c x y s t}
      ‚Üí Œì ‚ä¢ c
      ---------------------------
      ‚Üí (x ‚¶Ç (y ‚¶Ç s ‚Üí' t) ‚à∑ Œì) ‚ä¢ c
```

This rule is based on the subtyping algorithm presented in the paper, which also ignores function types.

## Non-capture-avoiding Œ±-renaming

The Œ±-renaming defined in the paper ([@fig:subst-defn]) is not capture-avoiding.

In the last case, if $x = z$, then all free occurrences of $y$ in $t$ are translated to bound $x$.

This is fixed by the new algorithm in [@sec:the-new-algorithm].

## Missing Œ±-renaming

The type systems in the paper are over-reliant on variable name equality.

For example, programs won't type-check if they contain types like:

::: hidden :::
```agda
module Example-Needs-CAŒ±R where
  open import Lang-ŒªŒ≤.Type
  open import Lang-ŒªŒ≤.Typing
  open import Lang-ŒªŒ≤.Refinement
  _‚Ü¶_ = _:=_
  infixl 50 _‚Ü¶_
  int2int : Type
  int2int =
```
::::::::::::::
```agda
    "x"‚¶Ç int ‚ü®"y"‚à£ true ‚ü© ‚Üí' int ‚ü®"z"‚à£ true ‚ü©
```

This is because the [Ent-Ext]{.smallcaps} rule ([@fig:rules-ent-subty], yellow) requires that the name bound *inside* the refinement ("y") be the same as the one *outside* the refinement ("x").

This is easily fixed by adding Œ±-renaming to the [Ent-Ext]{.smallcaps} rule:

::: hidden :::
```agda
  data _‚ä¢_ : Context ‚Üí Untyped-Constraint ‚Üí Set where
```
::::::::::::::
```agda
    ent-ext
      : ‚àÄ{Œì c x v b p}
      ‚Üí Œì ‚ä¢ (‚àÄ‚àÄ x ‚¶Ç b2p b ‚àô (p [ v ‚Ü¶ x ]·µ£ ‚áí c))
      ----------------------------------------
      ‚Üí (x ‚¶Ç b ‚ü® v ‚à£ p ‚ü© ‚à∑ Œì) ‚ä¢ c
```

## Non-unique typing derivations

The type-checking rule for `if` statements ([@fig:rule-if]) contains an alarming "y is fresh" side-condition.

![The "y is fresh" side condition](images/rule-if.png){#fig:rule-if}

This is very awkward side condition, for both implementations and meta-theory, since:

1. Free variables must be explicitly calculated
2. The (very loose) side condition acts as a *choice point*: any typing judgement involving an `if` statement may have an *infinite* number of typing derivations (rather than at most one), which makes writing decision procedures more difficult.

I avoided this can of worms by allowing contexts to contain raw refinements (alongside ordinary variable-type bindings).

This allows the type-checking rule for "if" to add its constraints to contexts without needing to invent a dummy variable:

::: hidden :::
```agda
module Raw-Refs where
  open import Lang-ŒªŒ≤.Refinement
  open import Lang-ŒªŒ≤.Type
  open import Lang-ŒªŒ≤.Term
  open import Lang-ŒªŒ≤.Typing
  data _‚ä¢_‚áê·µó_ (Œì : Context) : Term ‚Üí Type ‚Üí Set where
```
::::::::::::::

```agda
    chk-if
      : ‚àÄ {x e‚ÇÅ e‚ÇÇ t}
      ‚Üí Œì ‚ä¢ var x ‚áê·µó (bool ‚ü®‚ü©)
      ‚Üí (raw (var x ‚ñπ == ‚óÉ true ) ‚à∑ Œì) ‚ä¢ e‚ÇÅ ‚áê·µó t
      ‚Üí (raw (var x ‚ñπ == ‚óÉ false) ‚à∑ Œì) ‚ä¢ e‚ÇÇ ‚áê·µó t
      ------------------------------------------
      ‚Üí Œì ‚ä¢ if x then e‚ÇÅ else e‚ÇÇ ‚áê·µó t
```

# Conclusion, Future Work

In this report, I've shown off the highlights of an Agda formalization of refinement typing, including a list of bugs and a bag of tricks such as mocking SMT and pure capture-avoiding Œ±-renaming.

However, I didn't try:

The final 6 languages

: in the paper, which include advanced features such as ML-style polymorphism, user-defined data types, and a richer refinement language.

Connecting a real SMT solver

: to replace the mock, which needs to be expanded for each new example.

Tidying up algorithmic typing

: which is currently implemented using an ad-hoc jumble of `map`$‚Ä≤$, the `id` monad, and `with` syntax. An advanced EDSL for writing decision procedures could really shine here.

# Bibliography

::: {#refs}
:::
